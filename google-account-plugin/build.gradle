// Use IntelliJ src structure for now
sourceSets.main.java.srcDirs = ['src']
sourceSets.main.resources.srcDirs = ['resources']
sourceSets.test.java.srcDirs = ['testSrc']
sourceSets.test.resources.srcDirs = ['testResources']

intellij {
    pluginName = 'google-account'
}

dependencies {
    compile(project(':common-lib')) {
        transitive = true
    }

    compile files('lib/google.gdt.eclipse.login.common.jar')
    compile files('lib/google-gct-login-context-ij-pg.jar')
    compile fileTree(dir: 'third_party/repackaged', include: '*.jar')

    compile 'com.google.apis:google-api-services-oauth2:v2-rev70-1.18.0-rc'
    compile 'com.google.http-client:google-http-client-jackson:1.18.0-rc'
    compile 'javax.servlet:javax.servlet-api:3.0.1'
    compile 'net.jcip:jcip-annotations:1.0'
    runtime 'org.codehaus.jackson:jackson-core-asl:1.9.11'

    testCompile 'xalan:serializer:2.7.1'
    testCompile files("${System.properties['java.home']}/../lib/tools.jar")
}

processResources {
    def trackerProperty =  System.getenv("ANALYTICS_ID");
    if (trackerProperty) {
        inputs.property("usageTrackerProperty", trackerProperty)
        filesMatching("**/config.properties") {
            expand "usageTrackerProperty": trackerProperty
        }
    }
}