def ideaHome = '../idea-IC/'
def pluginName = 'google-cloud-tools-core'

dependencies {
    compile project(':google-login-plugin')

    compile files('lib/google-api-services-developerprojects-v1-rev20140815212553-1.19.0.jar')
    compile files('lib/google-api-services-source.jar')
    compile files(ideaHome + '/plugins/git4idea/lib/git4idea.jar')
    compile files(ideaHome + '/lib/bootstrap.jar')
    compile files(ideaHome + '/lib/util.jar')
    compile fileTree(dir: ideaHome + '/lib')
    compile fileTree(dir: ideaHome + '/plugins/Groovy/lib')
    compile fileTree(dir: ideaHome + '/plugins/gradle/lib')

    compile 'com.google.appengine:gradle-appengine-builder-model:0.1.0'
    compile 'org.eclipse.jgit:org.eclipse.jgit:3.3.0.201403021825-r'
    compile 'com.google.apis:google-api-services-clouddebugger:v2-rev4-1.20.0'
    // todo(elharo): we shouldn't need Apache commons; Guava will suffice
    compile 'commons-io:commons-io:2.4'
    compile 'org.jdom:jdom:2.0.2'

    testCompile files('third_party/jackson-core-asl/jackson-core-asl-1.9.11.jar')
    testCompile files(ideaHome + '/plugins/git4idea/lib/remote-servers-git.jar')
    testCompile files(ideaHome + '/plugins/git4idea/lib/resources_en.jar')
    testCompile files(ideaHome + '/plugins/properties/lib/properties.jar')
    testCompile files(ideaHome + '/plugins/gradle/lib/gradle-tooling-api-2.2.jar')
    testCompile files(ideaHome + '/plugins/junit/lib/idea-junit.jar')
    testCompile files("${System.properties['java.home']}/../lib/tools.jar")

    // todo(appu): Should we be using + values here? Will that lead to a mismatch with what intellij is configuring?
    testCompile 'junit:junit:4.+'
    testCompile 'org.eclipse.jgit:org.eclipse.jgit.junit:3.0.+'
    testCompile 'org.mockito:mockito-all:1.9.5'

    testRuntime files(ideaHome + '/lib/idea_rt.jar')
    testRuntime files(ideaHome + '/lib/trove4j.jar')
}

sourceSets {
    main {
        java {
            srcDirs 'src', 'resources'
        }
        resources {
            srcDir 'resources'
        }
    }
    test {
        java {
            srcDirs 'testSrc'
        }
    }
}

test {
    testLogging {
        events "started", "passed", "skipped", "failed", "standardOut", "standardError"
    }
}

// because plugin.xml doesn't live in resources
jar {
    baseName = pluginName
    version = ''
    from ('./src') {
        include 'META-INF/plugin.xml'
        include 'META-INF/android-studio-features.xml'
    }
}

task plugin(type: Zip,  dependsOn: jar) {
    baseName = pluginName
    destinationDir = file('build')
    include '*.jar'
    from('build/libs') {
        into(pluginName + '/lib')
    }
    from('lib/') {
        into(pluginName + '/lib')
    }
    from('third_party/gradle-appengine-builder-model') {
        into(pluginName + '/lib')
    }
}

findbugs {
    ignoreFailures = true
    sourceSets = [sourceSets.main]
    effort = "max"
}

tasks.withType(FindBugs) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

pmd {
    ignoreFailures = true
    sourceSets = [sourceSets.main]
}
