/*
 * Copyright 2017 Google Inc. All Rights Reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

// Use IntelliJ src structure for now
sourceSets.main.java.srcDirs = ['src']
sourceSets.main.resources.srcDirs = ['resources']
sourceSets.test.java.srcDirs = ['testSrc']
sourceSets.test.resources.srcDirs = ['testResources']

dependencies {
    compile(project(':common-lib'))
    compile files('lib/google.gdt.eclipse.login.common.jar')
    compile files('lib/google-gct-login-context-ij-pg.jar')
    compile files('third_party/repackaged/google-api-java-client-min-repackaged-1.20.0.jar')

    // TODO : see #350
    compile 'com.google.http-client:google-http-client-jackson:1.20.0'
    compile 'org.codehaus.jackson:jackson-core-asl:1.9.11'

    compile 'com.google.apis:google-api-services-oauth2:v2-rev98-1.20.0'
    compile 'javax.servlet:javax.servlet-api:3.0.1'

    testCompile 'xalan:serializer:2.7.1'
    testCompile files("${System.properties['java.home']}/../lib/tools.jar")
    testCompile(project(':common-test-lib'))
}

jar.archiveName = "google-account.jar"

configurations {
    // these are already obtained from google-api-java-client-min-repackaged
    compile.exclude group: 'com.google.api-client', module: 'google-api-client'
    compile.exclude group: 'com.google.http-client', module: 'google-http-client'
}

processResources {
    def trackerProperty =  System.getenv("ANALYTICS_ID")
    if (trackerProperty) {
        inputs.property("usageTrackerProperty", trackerProperty)
        filesMatching("**/config.properties") {
            expand "usageTrackerProperty": trackerProperty
        }
    }
}