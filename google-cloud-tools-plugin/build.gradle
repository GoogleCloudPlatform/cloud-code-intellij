/*
 * Copyright (C) 2016 The Android Open Source Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

// Use IntelliJ src structure for now
sourceSets.main.java.srcDirs = ['src']
sourceSets.main.resources.srcDirs = ['resources']
sourceSets.test.java.srcDirs = ['testSrc']
sourceSets.test.resources.srcDirs = ['testResources']

intellij {
    pluginName = 'google-cloud-tools'
    plugins 'Groovy','gradle', 'git4idea', 'properties', 'junit'

    publish {
        pluginId '8079'
    }
}

project.afterEvaluate {
    prepareSandbox.dependsOn ':google-account-plugin:prepareSandbox'
}

dependencies {
    compile(project(':google-account-plugin'))
    compile(project(':common-lib'))

    compile 'com.google.apis:google-api-services-clouddebugger:v2-rev7-1.21.0'
    compile 'com.google.apis:google-api-services-cloudresourcemanager:v1beta1-rev12-1.21.0'
    compile files('lib/google-api-services-source.jar')

    testCompile(project(':common-test-lib'))
    testRuntime files('../google-account-plugin/lib/google-gct-login-context-ij-pg.jar')
}

configurations {
    // this is already obtained from google-api-java-client-min-repackaged
    compile.exclude group: 'com.google.api-client', module: 'google-api-client'
}

test {
    testLogging {
        events "skipped", "failed"
    }
    doLast{
       println "View report at file://$buildDir/reports/tests/index.html"
    }
}

