/*
 * Copyright 2017 Google Inc. All Rights Reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

// Use IntelliJ src structure for now
sourceSets.main.java.srcDirs = ['src']
// 'src' is included in resources to export .form files to upstream modules.
sourceSets.main.resources.srcDirs = ['resources', 'src']
sourceSets.test.java.srcDirs = ['testSrc']
sourceSets.test.resources.srcDirs = ['testResources', 'testData']

apply plugin: 'org.jetbrains.intellij'

intellij {
    pluginName = 'google-cloud-tools'
    plugins 'Groovy', 'gradle', 'git4idea', 'properties', 'junit', 'maven', 'yaml'
    patchPluginXml {
        changeNotes = project (":google-cloud-tools-plugin").file("change_notes.html")
                .getText('UTF-8')
    }
}

prepareSandbox {
    copy {
        from project(":google-cloud-tools-plugin").file("disabled_plugins.txt")
        into new File(project(":google-cloud-tools-plugin").buildDir, "idea-sandbox/config")
    }
}

publishPlugin {
    username System.getenv('IJ_REPO_USERNAME')
    password System.getenv('IJ_REPO_PASSWORD')
    channels ijPluginRepoChannel
}


dependencies {
    compile(project(':google-cloud-core'))
    compile(project(':google-cloud-storage'))
    compile(project(':google-cloud-repos'))
    compile(project(':google-cloud-apis'))
    compile(project(':google-cloud-apis:java-apis'))
    compile(project(':google-cloud-apis:java-apis:maven-apis'))
    compile(project(':app-engine:java'))
    compile(project(':app-engine:java:maven'))
    compile(project(':app-engine:java:gradle'))
    compile(project(':app-engine:java:ultimate'))
    compile(project(':stackdriver-debugger'))

    compile(project(':google-container-tools-intellij'))
    compile(project(':google-container-tools-intellij:core'))
    compile(project(':google-container-tools-intellij:skaffold'))
    compile(project(':google-container-tools-intellij:skaffold-editing'))

    compile 'com.google.protobuf:protobuf-java:2.5.0'
    compile 'com.google.apis:google-api-services-clouddebugger:v2-rev7-1.21.0'
    compile 'com.google.apis:google-api-services-cloudresourcemanager:v1beta1-rev12-1.21.0'
    compile 'com.google.apis:google-api-services-iam:v1-rev225-1.22.0'
    compile 'com.google.apis:google-api-services-appengine:v1-rev8-1.22.0'
    compile 'com.google.apis:google-api-services-servicemanagement:v1-rev374-1.22.0'
    compile ('com.google.cloud.tools:appengine-plugins-core:' + toolsLibVersion) {
        exclude group: 'com.google.guava', module: 'guava'
        exclude group: 'org.yaml', module: 'snakeyaml'
    }
    compile 'org.yaml:snakeyaml:1.18'

    compileOnly 'com.google.auto.value:auto-value:1.4.1'
    apt         'com.google.auto.value:auto-value:1.4.1'

    testCompile(project(':common-test-lib'))
    testCompile(project(':google-container-tools-intellij:common-test-lib'))
    testCompile project(':stackdriver-debugger')
    testRuntime files('google-account/lib/google-gct-login-context-ij-pg.jar')
}

task testJar(type: Jar, dependsOn: testClasses) {
    baseName = "${project.archivesBaseName}-tests"
    from sourceSets.test.output
}

configurations {
    // this is already obtained from google-api-java-client-min-repackaged
    compile.exclude group: 'com.google.api-client', module: 'google-api-client'
    tests
}

artifacts {
    tests testJar
}

test {
    systemProperty "idea.log.leaked.projects.in.tests", System.getProperty("idea.log.leaked.projects.in.tests")

    testLogging {
        events "skipped", "failed"
        exceptionFormat "full"
        showCauses true
    }
    doLast{
       println "IDEA leaked project check: " + System.getProperty('idea.log.leaked.projects.in.tests')
       println "View report at file://$buildDir/reports/tests/index.html"
    }
}

