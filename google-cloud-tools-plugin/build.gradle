/*
 * Copyright 2017 Google Inc. All Rights Reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

// Use IntelliJ src structure for now
sourceSets.main.java.srcDirs = ['src']
sourceSets.main.resources.srcDirs = ['resources']
sourceSets.test.java.srcDirs = ['testSrc']
sourceSets.test.resources.srcDirs = ['testResources', 'testData']

apply plugin: 'org.jetbrains.intellij'

intellij {

    pluginName = 'google-cloud-tools'
    plugins 'Groovy', 'git4idea', 'gradle', 'properties', 'junit', 'maven', 'yaml'
}

publishPlugin {
    username System.getenv('IJ_REPO_USERNAME')
    password System.getenv('IJ_REPO_PASSWORD')
    channels ijPluginRepoChannel
}

project.afterEvaluate {
    //todo: currently we need this to include 'ultimate' module to the plugin zip; it would be better to support such inclusion in 'intellij' plugin
    def prepareSandboxTask = project.tasks.findByName("prepareSandbox")
    prepareSandboxTask.dependsOn project('ultimate').jar

    prepareSandboxTask.doLast {
        copy {
            from files(project('ultimate').jar)
            into "$prepareSandboxTask.destinationDir/$intellij.pluginName/lib"
        }
    }
}

dependencies {
    compile(project(':google-cloud-tools-plugin:google-cloud-core'))

    compile 'com.google.apis:google-api-services-clouddebugger:v2-rev7-1.21.0'
    compile 'com.google.apis:google-api-services-cloudresourcemanager:v1beta1-rev12-1.21.0'
    compile 'com.google.apis:google-api-services-iam:v1-rev225-1.22.0'
    compile 'com.google.apis:google-api-services-appengine:v1-rev8-1.22.0'
    compile ('com.google.cloud:google-cloud-storage:1.2.0') {
        exclude group: 'com.google.http-client', module: 'google-http-client'
        exclude group: 'com.google.http-client', module: 'google-http-client-jackson'
        exclude group: 'com.google.code.findbugs', module: 'jsr305'
        exclude group: 'com.google.guava', module: 'guava'
        exclude group: 'com.google.oauth-client', module: 'google-oauth-client'
        exclude group: 'com.google.protobuf', module: 'protobuf-java'
        exclude group: 'com.google.protobuf', module: 'protobuf-java-util'
    }
    compile 'com.google.apis:google-api-services-servicemanagement:v1-rev374-1.22.0'
    compile ('com.google.cloud.tools:appengine-plugins-core:' + toolsLibVersion) {
        exclude group: 'com.google.guava', module: 'guava'
        exclude group: 'org.yaml', module: 'snakeyaml'
    }
    compile files('lib/google-api-services-source.jar')
    compile 'org.yaml:snakeyaml:1.18'

    compileOnly 'com.google.auto.value:auto-value:1.4.1'
    apt         'com.google.auto.value:auto-value:1.4.1'

    testCompile(project(':common-test-lib'))
    testRuntime files('google-account/lib/google-gct-login-context-ij-pg.jar')
}

task testJar(type: Jar, dependsOn: testClasses) {
    baseName = "${project.archivesBaseName}-tests"
    from sourceSets.test.output
}

configurations {
    // this is already obtained from google-api-java-client-min-repackaged
    compile.exclude group: 'com.google.api-client', module: 'google-api-client'
    tests
}

processResources {
    def trackerProperty =  System.getenv("ANALYTICS_ID")
    if (trackerProperty) {
        inputs.property("usageTrackerProperty", trackerProperty)
        filesMatching("**/config.properties") {
            expand "usageTrackerProperty": trackerProperty
        }
    }
}

artifacts {
    tests testJar
}

test {
    systemProperty "idea.log.leaked.projects.in.tests", System.getProperty("idea.log.leaked.projects.in.tests")

    testLogging {
        events "skipped", "failed"
        exceptionFormat "full"
        showCauses true
    }
    doLast{
       println "IDEA leaked project check: " + System.getProperty('idea.log.leaked.projects.in.tests')
       println "View report at file://$buildDir/reports/tests/index.html"
    }
}

